# ARC: Memory Map
- Memory map includes more than just main memory (RAM)
- Includes both RAM and I/O

# Printing to Console

 - The console data port is at `0xFFFF0000`

```
0xFFFF0000
0b 1111 1111 1111 1111 0000 0000 0000 0000
```

- 22 high bits of `0xFFFF0000 = 0x3FFFC0`
- Load this address using `sethi`


```
		.begin
BASE    .equ 0x3fffc0
		.org 2048
main:   sethi BASE, %r3
		or %r0, 0x48, %r7
		stb %r7, %r3
		halt
		.end
```

- %r3 holds address of console output data port
- Set %r7 to ASCII 'H'
- Store a single byte to %r3

![[Pasted image 20250306110626.png|400]]

- Cannot send the next char until the console is done with the current one
- The console indicates when it is ready to receive another char

Console Ports
- Console data port is at `0xffff0000`
- Console status port is at `0xffff0004`

- Check if the console is read by polling the status port at bit position 7

`1000 0000`

- Load contents into a register, check status bit before storing to data port
	- 1 = ready
	- 0 = not ready

# Printing to Console V2: Status Port

```
		.begin
BASE    .equ 0x3fffc0
		.org 2048
main:   sethi BASE, %r2
		add %r2, 0x4, %r3
```

 - %r2 holds address of console output data port
 - Add %r2 to immediate constant 0x4, which is address of the status port

- BASE is the address of the start of the memory mapped region
- COUT is the offset of the data port
- CSTAT is the offset to status port

```
		.begin
BASE    .equ 0x3fffc0
COUT    .equ 0x0
CSTAT   .equ 0x4
		.org 2048
main:   sethi BASE, %r1
wait:   ldub [%r1 + CSTAT], %r2
		and %r2, 0x80, %r2
		be wait
		or %r0, 0x48, %r7
		stb %r7, [%r1 + COUT]
		.end
```

- output the char 'H'

# Printing to Console V3: Full String

- Print a string (sequence of individual characters)
- Test the status bit before writing each character

```
		.begin
BASE    .equ 0x3fffc0
COUT    .equ 0x0
CSTAT   .equ 0x4
		.org 2048

main:   sethi BASE, %r1
		clr %r2

loop:   ld [str + %r2], %r3
		orcc %r3, %r0, %r0
		be end

wait:   ldub [%r1 + CSTAT], %r6
		and %r6, 0x80, %r6
		be wait

		stb %r3, [%r1 + COUT]
		and %r2, 4, %r2
		ba loop

d:      halt

		.org 3000
		! "Hello, world!\n"
str:      0x48, 0x65, 0x6c, 0x6c, 0x6f
		0x2c, 0x20, 0x77, 0x6f, 0x72
		0x6c, 0x64, 0x21, 0x0a, 0	
```

- The string resides in memory starting at address 3000
- Each character will be stores as the LSB of a whole word
- String is terminated by 0

- %r2 holds string index
- Load each character from address `str + %r2`
- Test of null character
- `wait` test status bit before writing character
- `stb` stores character in console data port
- The string index is incremented
- Branched back to loop, until null character

Algorithm:
- Load character from current string index
- If NULL, branch to end
- If not NULL, wait for console status to be set
- Once set, write character to console data port
- Repeat

# User Input

- Keystrokes are places into a location in memory-mapped I/O
- Load them from memory the same way for anything else

- Keyboard data port is at `0xffff0008`
- Keyboard status port is at `0xffff000C`

```
		.begin
BASE    .equ 0xfffc0
COUT    .equ 0x0
CSTAT   .equ 0x4
CIN     .equ 0x8 <= keyboard data
CICTL   .equ 0xc <= keyboard status
		.org 2048

main:   clr %r1
		sethi BASE, %r1

iwait:  ldub [%r1 + CICTL], %r2
		andcc %r2, 0x80, %r2
		be iwait
		ldub [%r1 + CIN], %r3

owait:  ldub [%r1 + CSTAT], %r6
		andcc %r6, 0x80, %r6
		be owait
		stb %r3, [%r1 + COUT]

end:    halt
		.end
```

- Base and offsets for keyboard and console I/0 are initialized
- %r1 is set to BASE
- Load input status byte
- Check for character waiting, same bit as console output `0x80`
- Loop until input is read
- Load input into %r3

Algorithm:
- Program will stay in busy-wait loop until a keystroke is entered
- Keystrokes are entered in the console, the same place output appears
- The ASCII value of the keystroke will be stored in %r3
- Program halt after a single character is entered and printed
iwait
- Waits for keyboard input
- Loads value into %r3 when available
owait
- Wait for console to be ready
- Stores %r3 to console data port


# Read and Print All Characters

- The above code is wrapped in a loop, and will exit on ESC

```
		.begin
BASE    .equ 0xfffc0
COUT    .equ 0x0
CSTAT   .equ 0x4
CIN     .equ 0x8 <= keyboard data
CICTL   .equ 0xc <= keyboard status
		.org 2048

main:   clr %r1
		sethi BASE, %r1

iwait:  ldub [%r1 + CICTL], %r2
		andcc %r2, 0x80, %r2
		be iwait
		ldub [%r1 + CIN], %r31

		subcc %r31, 27, %r0 <= ESC
		be end

owait:  ldub [%r1 + CSTAT], %r6
		andcc %r6, 0x80, %r6
		be owait
		stb %r31, [%r1 + COUT]

		ba iwait

end:    halt
		.end
```


# Busy-Waiting

```
iwait:  ldub [%r1 + CICTIL], %r2
		andcc %r2, 0x80, %r2
		be iwait
```

- Busy waiting is what we did while waiting for keyboard input
- This is a bad idea as the processor is much faster at processing input then we are at entering it
- If the processor accomplishes nothing while waiting for input, most of its cycles are waster

# Programmed I/0

- Busy-waiting is what we are doing, but this type of I/O is called Programmed I/O
- CPU must actively poll each device to check if data is ready
- Processor periodically checks that status of I/O modules
- The keyboard can raise an interrupt, to let the processor know it is read (busy waiting, active polling)

# Interrupts and Traps

- An interrupt is a hardware signal generated by an external device
- Interrupts occur asynchronously
- They are independent of the current instruction being executed

Examples:
- Keyboard can generate interrupts when a key is pressed
- Secondary storage devices can generate interrupts when data is ready
- The system timer can generate interrupts periodically, indicate it is time to switch processes

How can the CPU react to an interrupt?
- The PC is updated according to the logic of current program
- Interrupts are detected in hardware, not machine instructions
- An electrical signal is sent along a trace of wire
- Modern operating systems assign IRQs (Interrupt ReQuest channels) to devices

# Handling Interrupts

- **Fetching** refers to the retrieval of the next instruction from the address stored in the program counter
- **Execution** involves decoding the instruction and performing whatever is required as indicated by the opcode

# Interrupt-Driven I/O

- When an interrupt is pending, and interrupts are enabled
	- It will branch to the appropriate interrupt handler
	- PC will be updated to reflect the start of the handler
	- Next instruction fetched is from the handler, not the user program
	- When handler finishes, return to user program
- Instead of polling devices, have then issue interrupts
	- Processor ignores device until it issues interrupt
	- No more time wasted on active polling
	- Data to be transferred still goes through processor
	- Add this address with _Direct Memory Access_

# Interrupt Service Routines

- Different interrupts result in different handlers being invoked
- _Interrupt Service Routine (ISR)_
- One ISR for keyboard, one for hard disk, one for system timer, etc.
- ISRs are simple subroutines and are provided by the operating system

# Disabling Interrupts

- We may not want a time-sensitive program being interrupted
- ISAs include instructions for enabling and disabling interrupts
- This is indicated by the _Interrupt Flag (IF)_ in the PSR

# Interrupts vs Traps

Interrupt
- An interrupt is a hardware signal generated by an external device
- Interrupts occur asynchronously
- They are independent of the current instruction being executed

Trap
- A trap occurs as a result of the current instruction being executed
- Condition making it impossible for CPU to continue executing without software intervention
- Exception in user program that bust be handled
- Definition of traps, interrupt, and exception overlap

# Traps/Exceptions

Examples
- Divide by zero
- Illegal memory access
- Illegal instruction

- Traps result in the execution of a subroutine, like ISRs
- Traps place the system in supervisor mode (or Kernel mode)
- This means the trap subroutine can executed privileged instructions

# User vs Supervisor Mode

User Mode:
- For user programs
- Certain areas of memory are off limits
- Certain instructions may not be executed

Kernel Mode:
- OS executed in this mode
- Privileged instructions may be executed
- Protected areas of memory may be accessed

# ARC: Traps

- `ta` - trap always
- `rett` - return from trap

`ta software_trap_number`

- Software trap number range [0, 127]
- 7 bits, unsigned
- Can be 7 low order bits in a register or immediate constant
![[Pasted image 20250306122905.png|500]]
![[Pasted image 20250306122835.png|500]]

- In ARC, there is only one trap always

ta 0
- Trap number 0 is a call to the OS for some service
- The kind of system service is specified by an integer stored in %r1
- ARC tool is not an operating system, to these services do not exist
- The trap address is constructed in the _Trap Base Register (TBR)_
- However, does not jump to the subroutine
- _Trap Base Address (TBA)_ forms the base of the trap table
- This is where traps are found in memory
- _Trap type (tt)_ 8th bit is set to 1
	- Range of [128-255]
- Last 4 bits are set to 0

![[Pasted image 20250306123347.png|500]]

Execute a trap instruction:
- If _enable traps (ET)_ bit is set, clear it
- Store PC in %r16, PSR in %r18
- Construct TBR
- TBR get written into program counter, and will jump to and being executing trap routine
- A new trap routine begins every four instructions
	- $2^4 = 16$ bytes, 4 bytes per instruction, 4 instructions

```
		.begin
		.org 2048
main:   ld [x], %r2
		or %r0, %r2, %r3
		addcc %r2, %r3, %r0
		ta 32
		halt
x:      2000000000

		.org 0xff000a00
trap:   rett %16, 4
		.end
```

- The `andcc` causes and overflow and negative result
- ta 32 results in TBR being built
- %r16 and %r18 set to address of trap instruction
- The trap returns to address `%r16+4`, to halt